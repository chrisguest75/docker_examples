set dotenv-load := true

# default lists actions
default:
  @just -f justfile --list

build_image distro *args:
  docker build --network host --progress=plain -f Dockerfile.{{ distro }} {{ args }} -t 10_distro_versions_{{ distro }} .

start distro *args: (build_image distro args)
  docker run --net host -it --rm 10_distro_versions_{{ distro }}
  @echo "******************************"
  @echo "** Labels"
  @echo "******************************"
  docker inspect -f '{{{{.Config.Labels}}}}' 10_distro_versions_{{ distro }}
  @echo "******************************"
  @echo "** Dive CI"
  @echo "******************************"
  dive 10_distro_versions_{{ distro }} --ci || true
  @echo "******************************"
  @echo "** Size"
  @echo "******************************"
  container-diff analyze --json daemon://10_distro_versions_{{ distro }}:latest | jq .
  #docker buildx imagetools inspect --raw 10_distro_versions_{{ distro }}:latest

debug distro: (build_image distro)
  docker run --net host -it --rm --entrypoint /bin/sh 10_distro_versions_{{ distro }}
